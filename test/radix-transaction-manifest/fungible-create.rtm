# Locking 10 XRD in fees from the account component. 
CALL_METHOD 
    Address("account_tdx_2_1294v3qk9kg2v9q2xtgsv3yeqzudev9kkew93jqha8phcg6wsz0tzd4") 
    "lock_fee"
    Decimal("500");

# Creating a new resource with a divisibility of 18 and a name of `MyResource`. The resource has 
# default resource behavior where it can be withdrawn and deposited by anybody.
CREATE_FUNGIBLE_RESOURCE_WITH_INITIAL_SUPPLY
    # Owner role - This gets metadata permissions, and is the default for other permissions
    # Can set as Enum<OwnerRole::Fixed>(access_rule)  or Enum<OwnerRole::Updatable>(access_rule)
    Enum<OwnerRole::None>()
    true             # Whether the engine should track supply (avoid for massively parallelizable tokens)
    0u8             # Divisibility (between 0u8 and 18u8)
    Decimal("100000000") # Initial supply
    Tuple(
        None,        # Mint Roles (if None: defaults to DenyAll, DenyAll)
        # Some(         # Mint Roles (if None: defaults to DenyAll, DenyAll)
        #     Tuple(
        #         Some(Enum<AccessRule::AllowAll>()),  # Minter (if None: defaults to Owner)
        #         Some(Enum<AccessRule::DenyAll>())    # Minter Updater (if None: defaults to Owner)
        #     )
        # ),
        None,        # Burn Roles (if None: defaults to DenyAll, DenyAll)
        None,        # Freeze Roles (if None: defaults to DenyAll, DenyAll)
        None,        # Recall Roles (if None: defaults to DenyAll, DenyAll)
        None,        # Withdraw Roles (if None: defaults to AllowAll, DenyAll)
        None         # Deposit Roles (if None: defaults to AllowAll, DenyAll)
    )
    Tuple(                                                                   # Metadata initialization
        Map<String, Tuple>(                                                  # Initial metadata values
            "name" => Tuple(
                Some(Enum<Metadata::String>("Test XRD")),                  # Resource Name
                true                                                         # Locked
            ),
            "symbol" => Tuple(
                Some(Enum<Metadata::String>("TXRD")),
                true
            ),
            "description" => Tuple(
                Some(Enum<Metadata::String>("For test")),
                true
            )
        ),
        Map<String, Enum>(                                                   # Metadata roles
            "metadata_setter" => None,                                      # Metadata setter role
            "metadata_setter_updater" => None,                               # Metadata setter updater role as None defaults to OWNER
            "metadata_locker" => None,                                      # Metadata locker role
            "metadata_locker_updater" => None                                # Metadata locker updater role as None defaults to OWNER
        )
    )
    None;             # No Address Reservation

# Depositing the entirety of the initial supply of the newly created resource into our account 
# component.
CALL_METHOD
    Address("account_tdx_2_1294v3qk9kg2v9q2xtgsv3yeqzudev9kkew93jqha8phcg6wsz0tzd4") 
    "deposit_batch"
    Expression("ENTIRE_WORKTOP");